SELECT DISTINCT
  ?item ?itemLabel
  (COUNT(DISTINCT ?article) AS ?count)
WHERE
{
    { ?item wdt:P106 wd:Q36180. } #occupation (P106) is writer (Q36180)
  UNION
    { ?item wdt:P106 wd:Q49757. } #occupation (P106) is poet (Q36180)

    {?item wdt:P27 wd:Q159.} #country of citizenship (P27) is Russia (Q159)
  UNION
    {?item wdt:P27 wd:Q34266.} # or Russian Empire (Q34266)
  UNION
    {?item wdt:P27 wd:Q15180.} # or Soviet Union
  UNION
    {?item wdt:P27 wd:Q1108445.} # or Ancient Russia

    {SELECT ?item ?article WHERE {
      ?article schema:about ?item.
      ?article schema:isPartOf / wikibase:wikiGroup "wikipedia" . # also wikisource, wikiqoute, wikibooks, etc.
    }}

  SERVICE wikibase:label { bd:serviceParam wikibase:language "ru". }
}
GROUP BY ?item ?itemLabel
ORDER BY DESC (?count)
